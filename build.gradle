import org.apache.tools.ant.filters.*

apply plugin: 'java'
apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: "com.github.johnrengelman.shadow"

group = 'me.elsiff'
version = '3.1.0-SNAPSHOT'

ext.pluginName = 'MoreFish'
ext.author = 'elsiff'
ext.website = 'https://elsiff.me'
ext.mainPackage = 'me.elsiff.morefish'
ext.mainClass = mainPackage + ".MoreFish"

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url "http://repo.citizensnpcs.co/" }
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://raw.github.com/FabioZumbi12/RedProtect/mvn-repo/' }
    maven { url 'https://nexus.keyle.de/content/groups/public/' }
    maven { url 'https://ci.ender.zone/plugin/repository/everything/' }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}
compileKotlin {
    kotlinOptions.javaParameters = true
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"

    implementation 'co.aikar:acf-core:0.5.0-SNAPSHOT'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.github.elsiff:egui:1.0.2-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib-API:4.2.1'
    compileOnly 'com.sk89q:worldguard:6.1'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.6'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.18-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.7.1'
    compileOnly 'com.github.mcMMO-Dev:mcMMO:fa1bbd2'
    compileOnly 'br.net.fabiozumbi12.RedProtect:RedProtect-Spigot:7.5.5'
}

configurations {
    testCompile.extendsFrom compileOnly
}

shadowJar {
    relocate 'co.aikar.commands', "${mainPackage}.acf"
}
build { dependsOn shadowJar }

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "pluginName"     : pluginName,
            "project.version": project.property("version"),
            "author"         : author,
            "website"        : website,
            "mainClass"      : mainClass
    ]
}